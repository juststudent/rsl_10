scheme repo_alg = class
    type
        File,       /* variant of a file */
        Program,    /* version of a program */
        Files,      /* collection of files */
        Project     /* project */

    value
        /* Generators */
        empty : Project,

        addFile    : File    >< Project -> Project,
        addProgram : Program >< Project -> Project,
        attachFile : File >< Program >< Project -~-> Project,
        addDep     : File >< File >< Project -~-> Project,

        /* Modifiers */
        detachFile : File >< Program >< Project -~-> Project,

        /* Observers */
        hasFile     : File >< Project -> Bool,
        hasProgram  : Program >< Project -> Bool,
        hasAttached : File >< Program >< Project -~-> Bool,
        dependsOn   : File >< File >< Project -~-> Bool,

        /* Extra functionality */
        /* Generators */
        autoAttachFile : File >< Program >< Project -> Project,

        /* Modifiers */
        autoDetachFile : File >< Program >< Project -~-> Project,

        /* Observers */
        backwardDeps : File >< Project -~-> Files,
        forwardDeps  : File >< Project -~-> Files

    axiom
        /* ========== OBSERVER-GENERATOR ========== */

        /* observer-empty */
        all f : File    :- ~hasFile(f, empty),
        all p : Program :- ~hasProgram(p, empty),

        /* observer-addFile */
        all proj : Project, f1, f2 : File :-
            hasFile(f2, addFile(f1, proj)) is f1 = f2 \/ hasFile(f2, proj)
            pre ~hasFile(f1, proj),
        all proj : Project, f : File, p : Program :-
            hasProgram(p, addFile(f, proj)) is hasProgram(p, proj)
            pre ~hasFile(f, proj),
        all proj : Project, f1, f2 : File, p : Program :-
            hasAttached(f2, p, addFile(f1, proj)) is f2 ~= f1 /\ hasAttached(f2, p, proj)
            pre ~hasFile(f1, proj) /\ hasProgram(p, proj) /\ (f2 ~= f1 => hasFile(f2, proj)),
        all proj : Project, f1, f2, f3 : File :-
            dependsOn(f2, f3, addFile(f1, proj)) is f2 ~= f1 /\ f3 ~= f1 /\ dependsOn(f2, f3, proj)
            pre ~hasFile(f1, proj) /\ (f2 ~= f1 => hasFile(f2, proj)) /\ (f3 ~= f1 => hasFile(f3, proj)),
        all proj : Project, f1, f2 : File :-
            backwardDeps(f2, addFile(f1, proj)) is backwardDeps(f2, proj)
            pre ~hasFile(f1, proj) /\ hasFile(f2, proj),
        all proj : Project, f1, f2 : File :-
            forwardDeps(f2, addFile(f1, proj)) is forwardDeps(f2, proj)
            pre ~hasFile(f1, proj) /\ hasFile(f2, proj),

        /* observer-addProgram */
        all proj : Project, p : Program, f : File :-
            hasFile(f, addProgram(p, proj)) is hasFile(f, proj)
            pre ~hasProgram(p, proj),
        all proj : Project, p1, p2 : Program :-
            hasProgram(p2, addProgram(p1, proj)) is p1 = p2 \/ hasProgram(p2, proj)
            pre ~hasProgram(p1, proj),
        all proj : Project, p1, p2 : Program, f : File :-
            hasAttached(f, p2, addProgram(p1, proj)) is p2 ~= p1 /\ hasAttached(f, p1, proj)
            pre ~hasProgram(p1, proj) /\ hasProgram(p2, proj) /\ (p2 ~= p1 => hasProgram(p2, proj)),
        all proj : Project, p : Program, f1, f2 : File :-
            dependsOn(f1, f2, addProgram(p, proj)) is dependsOn(f1, f2, proj)
            pre ~hasProgram(p, proj) /\ hasFile(f1, proj) /\ hasFile(f2, proj),
        all proj : Project, p : Program, f : File :-
            backwardDeps(f, addProgram(p, proj)) is backwardDeps(f, proj)
            pre ~hasProgram(p, proj) /\ hasFile(f, proj),
        all proj : Project, p : Program, f : File :-
            forwardDeps(f, addProgram(p, proj)) is forwardDeps(f, proj)
            pre ~hasProgram(p, proj) /\ hasFile(f, proj),

        /* observer-attachFile */
        all proj : Project, p : Program, f1, f2 : File :-
            hasFile(f2, attachFile(f1, p, proj)) is hasFile(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj) /\
                (all f : File :- (hasFile(f, proj) /\ dependsOn(f1, f, proj)) => hasAttached(f, p, proj)),
        all proj : Project, p1, p2 : Program, f1 : File :-
            hasProgram(p2, attachFile(f1, p1, proj)) is hasProgram(p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\
                (all f : File :- (hasFile(f, proj) /\ dependsOn(f1, f, proj)) => hasAttached(f, p1, proj)),
        all proj : Project, p1, p2 : Program, f1, f2 : File :-
            hasAttached(f2, p2, attachFile(f1, p1, proj)) is (f1 = f2 /\ p1 = p2) \/ hasAttached(f2, p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\
                (all f : File :- (hasFile(f, proj) /\ dependsOn(f1, f, proj)) => hasAttached(f, p1, proj)) /\
                hasFile(f2, proj) /\ hasProgram(p2, proj),
        all proj : Project, p : Program, f1, f2, f3 : File :-
            dependsOn(f2, f3, attachFile(f1, p, proj)) is dependsOn(f2, f3, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj) /\
                (all f : File :- (hasFile(f, proj) /\ dependsOn(f1, f, proj)) => hasAttached(f, p, proj)),
        all proj : Project, p : Program, f1, f2 : File :-
            backwardDeps(f2, attachFile(f1, p, proj)) is backwardDeps(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj) /\
                (all f : File :- (hasFile(f, proj) /\ dependsOn(f1, f, proj)) => hasAttached(f, p, proj)) /\
                hasFile(f2, proj),
        all proj : Project, p : Program, f1, f2 : File :-
            forwardDeps(f2, attachFile(f1, p, proj)) is forwardDeps(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj) /\
                (all f : File :- (hasFile(f, proj) /\ dependsOn(f1, f, proj)) => hasAttached(f, p, proj)) /\
                hasFile(f2, proj),

        /* observer-addDep */
        all proj : Project, f1, f2, f3 : File :-
            hasFile(f3, addDep(f1, f2, proj)) is hasFile(f3, proj)
            pre hasFile(f1, proj) /\ hasFile(f2, proj) /\
                (all p : Program :- (hasProgram(p, proj) /\ hasAttached(f1, p, proj)) => hasAttached(f2, p, proj)),
        all proj : Project, f1, f2 : File, p1 : Program :-
            hasProgram(p1, addDep(f1, f2, proj)) is hasProgram(p1, proj)
            pre hasFile(f1, proj) /\ hasFile(f2, proj) /\
                (all p : Program :- (hasProgram(p, proj) /\ hasAttached(f1, p, proj)) => hasAttached(f2, p, proj)),
        all proj : Project, f1, f2, f3 : File, p1 : Program :- 
            hasAttached(f3, p1, addDep(f1, f2, proj)) is hasAttached(f3, p1, proj)
            pre hasFile(f3, proj) /\ hasProgram(p1, proj) /\
                hasFile(f1, proj) /\ hasFile(f2, proj) /\
                (all p : Program :- (hasProgram(p, proj) /\ hasAttached(f1, p, proj)) => hasAttached(f2, p, proj)),
        all proj : Project, f1, f2, f3, f4 : File :-
            dependsOn(f3, f4, addDep(f1, f2, proj)) is (f1 = f3 /\ f2 = f4) \/ dependsOn(f3, f4, proj)
            pre hasFile(f3, proj) /\ hasFile(f4, proj) /\
                hasFile(f1, proj) /\ hasFile(f2, proj) /\
                (all p : Program :- (hasProgram(p, proj) /\ hasAttached(f1, p, proj)) => hasAttached(f2, p, proj)),

        /* observer-autoAttachFile */
        all proj : Project, p : Program, f1, f2 : File :-
            hasFile(f2, autoAttachFile(f1, p, proj)) is hasFile(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj),
        all proj : Project, p1, p2 : Program, f1 : File :-
            hasProgram(p2, autoAttachFile(f1, p1, proj)) is hasProgram(p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj),
        all proj : Project, p1, p2 : Program, f1, f2 : File :-
            hasAttached(f2, p2, autoAttachFile(f1, p1, proj)) is (f1 = f2 /\ p1 = p2) \/ hasAttached(f2, p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasFile(f2, proj) /\ hasProgram(p2, proj),
        all proj : Project, p : Program, f1, f2, f3 : File :-
            dependsOn(f2, f3, autoAttachFile(f1, p, proj)) is dependsOn(f2, f3, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj),
        all proj : Project, p : Program, f1, f2 : File :-
            backwardDeps(f2, autoAttachFile(f1, p, proj)) is backwardDeps(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj) /\ hasFile(f2, proj),
        all proj : Project, p : Program, f1, f2 : File :-
            forwardDeps(f2, autoAttachFile(f1, p, proj)) is forwardDeps(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p, proj) /\ hasFile(f2, proj),

        /* ========== OBSERVER-MODIFIER ========== */

        /* observer-detachFile */
        all proj : Project, f1, f2 : File, p1 : Program :-
            hasFile(f2, detachFile(f1, p1, proj)) is hasFile(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1 : File, p1, p2 : Program :-
            hasProgram(p2, detachFile(f1, p1, proj)) is hasProgram(p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1, f2 : File, p1, p2 : Program :-
            hasAttached(f2, p2, detachFile(f1, p1, proj)) is (f1 ~= f2 \/ p1 ~= p2) /\ hasAttached(f2, p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                hasFile(f2, proj) /\ hasProgram(p2, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1, f2, f3 : File, p1 : Program :-
            dependsOn(f2, f3, detachFile(f1, p1, proj)) is dependsOn(f2, f3, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1, f2 : File, p1 : Program :-
            backwardDeps(f2, detachFile(f1, p1, proj)) is backwardDeps(f2, proj)
            pre hasFile(f2, proj) /\
                hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1, f2 : File, p1 : Program :-
            forwardDeps(f2, detachFile(f1, p1, proj)) is forwardDeps(f2, proj)
            pre hasFile(f2, proj) /\
                hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),

        /* observer-autoDetachFile */
        all proj : Project, f1, f2 : File, p1 : Program :-
            hasFile(f2, autoDetachFile(f1, p1, proj)) is hasFile(f2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj),
        all proj : Project, f1 : File, p1, p2 : Program :-
            hasProgram(p2, autoDetachFile(f1, p1, proj)) is hasProgram(p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj),
        all proj : Project, f1, f2 : File, p1, p2 : Program :-
            hasAttached(f2, p2, autoDetachFile(f1, p1, proj)) is (f1 ~= f2 \/ p1 ~= p2) /\ hasAttached(f2, p2, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                hasFile(f2, proj) /\ hasProgram(p2, proj),
        all proj : Project, f1, f2, f3 : File, p1 : Program :-
            dependsOn(f2, f3, autoDetachFile(f1, p1, proj)) is dependsOn(f2, f3, proj)
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj),
        all proj : Project, f1, f2 : File, p1 : Program :-
            backwardDeps(f2, autoDetachFile(f1, p1, proj)) is backwardDeps(f2, proj)
            pre hasFile(f2, proj) /\
                hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj),
        all proj : Project, f1, f2 : File, p1 : Program :-
            forwardDeps(f2, autoDetachFile(f1, p1, proj)) is forwardDeps(f2, proj)
            pre hasFile(f2, proj) /\
                hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj),

        /* ========== MODIFIER-GENERATOR ========== */

        /* detachFile-generator */
        all proj : Project, f1, f2 : File, p1 : Program :-
            detachFile(f1, p1, addFile(f2, proj)) is addFile(f2, detachFile(f1, p1, proj))
            pre ~hasFile(f2, proj) /\
                hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1 : File, p1, p2 : Program :-
            detachFile(f1, p1, addProgram(p2, proj)) is addProgram(p2, detachFile(f1, p1, proj))
            pre ~hasProgram(p2, proj) /\
                hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1, f2 : File, p1, p2 : Program :-
            detachFile(f1, p1, attachFile(f2, p2, proj)) is
                if f1 = f2 /\ p1 = p2 then proj
                                      else attachFile(f2, p2, detachFile(f1, p1, proj)) end
            pre hasFile(f2, proj) /\ hasProgram(p2, proj) /\
                (all f : File :- (hasFile(f, proj) /\ dependsOn(f2, f, proj)) => hasAttached(f, p2, proj)) /\
                hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) => ~dependsOn(f, f1, proj)),
        all proj : Project, f1, f2, f3 : File, p1 : Program :-
            detachFile(f1, p1, addDep(f2, f3, proj)) is addDep(f2, f3, detachFile(f1, p1, proj))
            pre hasFile(f1, proj) /\ hasProgram(p1, proj) /\ hasAttached(f1, p1, proj) /\
                (all p : Program, f : File :-
                    (hasProgram(p, proj) /\ hasFile(f, proj) /\ hasAttached(f, p, proj)) =>
                    ~dependsOn(f, f1, proj) /\ (f1 ~= f3 \/ f ~= f2))

end
